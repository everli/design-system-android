apply plugin: 'maven-publish'
apply plugin: 'signing'

/**
 * Declare the sources artifact for the library.
 * This is really important - it will make sure that the source files are packaged along with the executable,
 * compiled code, so that your users can easily jump to the definitions that theyâ€™re calling into within their IDE
 */
task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  if (project.plugins.findPlugin("com.android.library")) {
    // For Android libraries
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
  } else {
    // For pure Kotlin libraries
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
  }
}

artifacts {
  archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        // The coordinates of the library
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION

        // Two artifacts, the `aar` (or `jar`) and the sources
        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        artifact androidSourcesJar

        // Mostly self-explanatory metadata
        pom {
          name = PUBLISH_ARTIFACT_ID
          description = 'Everli Design System Android SDK'
          url = 'https://github.com/everli/design-system-android'
          licenses {
            license {
              name = 'Everli License'
              url = 'https://github.com/everli/design-system-android/blob/dev/LICENSE.MD'
            }
          }
          developers {
            developer {
              id = 'GhimpuLucianEduard'
              name = 'Lucian Ghimpu'
              email = 'lucian.ghimpu@everli.com'
            }
            // add more devs here
          }

          // Version control ingo
          scm {
            connection = 'scm:git:github.com/everli/design-system-android.git'
            developerConnection = 'scm:git:ssh://github.com/everli/design-system-android.git'
            url = 'https://github.com/everli/design-system-android/tree/master'
          }
        }
      }
    }
  }
}

/**
 * Grab signing related values from the root project,
 * and then tells the signing plugin to sign the artifacts defined above as a publication
 */
ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
  sign publishing.publications
}
